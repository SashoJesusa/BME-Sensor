#include <ESP8266WiFi.h>
#include <ESP8266WebServer.h>
#include <PubSubClient.h>
#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BME680.h>

// Config
const char* ssid = "";
const char* password = "";

const char* mqtt_server = "";  
const char* mqtt_user = "";
const char* mqtt_pass = "";

WiFiClient espClient;
PubSubClient client(espClient);
ESP8266WebServer server(80);
Adafruit_BME680 bme;

unsigned long lastMsg = 0;

// WIFI connect
void setup_wifi() {
  Serial.print("Connecting to WiFi");
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nWiFi connected!");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());
}

// Home Assistant config
void announceToHA() {

  // Temperature
  client.publish(
    "homeassistant/sensor/sensor_temp/config",
    "{"
      "\"name\":\"Temperature\","
      "\"unique_id\":\"temp\","
      "\"state_topic\":\"sensor/temperature\","
      "\"unit_of_measurement\":\"°C\","
      "\"device_class\":\"temperature\","
      "\"device\":{"
        "\"identifiers\":[\"sensor_device\"],"
        "\"name\":\"Room Sensor\""
      "}"
    "}",
    true
  );

  // Humidity
  client.publish(
    "homeassistant/sensor/sensor_hum/config",
    "{"
      "\"name\":\"Humidity\","
      "\"unique_id\":\"humidity\","
      "\"state_topic\":\"sensor/humidity\","
      "\"unit_of_measurement\":\"%\","
      "\"device_class\":\"humidity\","
      "\"device\":{"
        "\"identifiers\":[\"sensor_device\"],"
        "\"name\":\"Room Sensor\""
      "}"
    "}",
    true
  );

  // Pressure
  client.publish(
    "homeassistant/sensor/sensor_pres/config",
    "{"
      "\"name\":\"Pressure\","
      "\"unique_id\":\"pressure\","
      "\"state_topic\":\"sensor/pressure\","
      "\"unit_of_measurement\":\"hPa\","
      "\"device_class\":\"pressure\","
      "\"device\":{"
        "\"identifiers\":[\"sensor_device\"],"
        "\"name\":\"Room Sensor\""
      "}"
    "}",
    true
  );

  // Gas
  client.publish(
    "homeassistant/sensor/sensor_gas/config",
    "{"
      "\"name\":\"Gas\","
      "\"unique_id\":\"gas\","
      "\"state_topic\":\"sensor/gas\","
      "\"unit_of_measurement\":\"kΩ\","
      "\"device\":{"
        "\"identifiers\":[\"sensor_device\"],"
        "\"name\":\"Room Sensor\""
      "}"
    "}",
    true
  );
}

// MQTT reconnect
void reconnect() {
  while (!client.connected()) {
    Serial.print("Trying MQTT... ");

    if (client.connect("room_sensor", mqtt_user, mqtt_pass)) {
      Serial.println("connected!");
      announceToHA();
    } else {
      Serial.print("failed, rc=");
      Serial.println(client.state());
      delay(2000);
    }
  }
}

// Web page
String page() {
  bme.performReading();

  String html = "<!DOCTYPE html><html><head>";
  html += "<meta charset='UTF-8'>";
  html += "<meta name='viewport' content='width=device-width, initial-scale=1'>";
  html += "<meta http-equiv='refresh' content='1'>";
  html += "<style>";
  html += "body{font-family:Arial;text-align:center;background:#fff;margin-top:20px;}";
  html += "h2{font-size:26px;font-weight:bold;margin-bottom:5px;}";
  html += ".title{font-size:28px;font-weight:bold;margin:10px 0 20px 0;}";
  html += ".sensor{font-size:20px;font-weight:bold;margin:8px 0;}";
  html += ".value{font-weight:normal;}";
  html += "</style></head><body>";

  html += "<h2>ESP BME680 Sensor</h2>";
  html += "<div class='title'>Room Sensor</div>";

  html += "<div class='sensor'>Temperature <span class='value'>" 
          + String(bme.temperature - 10.0) + " °C</span></div>";

  html += "<div class='sensor'>Humidity <span class='value'>" 
          + String(bme.humidity) + " %</span></div>";

  html += "<div class='sensor'>Pressure <span class='value'>" 
          + String(bme.pressure / 100.0) + " hPa</span></div>";

  html += "<div class='sensor'>Gas <span class='value'>" 
          + String(bme.gas_resistance / 1000.0) + " kΩ</span></div>";

  html += "</body></html>";
  return html;
}

void setup() {
  Serial.begin(115200);
  setup_wifi();

  client.setServer(mqtt_server, 1883);
  client.setBufferSize(1024);


  if (!bme.begin()) {
    Serial.println("BME680 not found!");
    while (1);
  }

  bme.setTemperatureOversampling(BME680_OS_8X);
  bme.setHumidityOversampling(BME680_OS_2X);
  bme.setPressureOversampling(BME680_OS_4X);
  bme.setIIRFilterSize(BME680_FILTER_SIZE_3);
  bme.setGasHeater(150, 50);

  server.on("/", []() { server.send(200, "text/html", page()); });
  server.begin();

  Serial.println("Web UI ready.");
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();

  float correctTemp = bme.temperature - 10.0;

  if (millis() - lastMsg > 5000) {
    lastMsg = millis();

    if (bme.performReading()) {

      client.publish("sensor/temperature", String(correctTemp).c_str(), true);
      client.publish("sensor/humidity", String(bme.humidity).c_str(), true);
      client.publish("sensor/pressure", String(bme.pressure / 100.0).c_str(), true);
      client.publish("sensor/gas", String(bme.gas_resistance / 1000.0).c_str(), true);

      Serial.println("MQTT updated!");
    }
  }

  server.handleClient();
}
